# $Id: PKGBUILD 74433 2010-03-30 22:54:24Z giovanni $
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: James Rayner <james@archlinux.org>
# Contributor:  Partha Chowdhury <kira.laucas@gmail.com>

pkgname=conky
pkgver=1.8.0
pkgrel=1live
pkgdesc="An advanced, highly configurable system monitor for X based on torsmo"
arch=('i686' 'x86_64')
url="http://conky.sourceforge.net/"
license=('custom')
replaces=('torsmo')
depends=('alsa-lib' 'libxml2' 'curl' 'wireless_tools' 'libxft' 'glib2' 'libxdamage' 'imlib2')
makedepends=('pkgconfig')
backup=(etc/conky/conky.conf etc/conky/conky_no_x11.conf)
source=(http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2
       conky-1.8.0-audacious-2.3.patch)
md5sums=('494cbaf1108cfdb977fc80454d9b13e2'
         '81a5b1d64eaa46a505b1c121044364ac')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/conky-1.8.0-audacious-2.3.patch" || return 1
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-hddtemp \
              --enable-mpd \
              --enable-xft \
              --enable-portmon \
              --enable-audacious=yes \
              --enable-wlan \
	      --enable-rss \
	      --enable-ibm \
	      --enable-imlib2 \
              --disable-lua
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
