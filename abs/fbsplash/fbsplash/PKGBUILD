# Maintainer: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Kurt J. Bosch <kujub [at] quantentunnel [dot] de>

pkgname=fbsplash
pkgver=1.5.4.3
pkgrel=20
pkgdesc="A userspace implementation of a splash screen for Linux (formerly known as gensplash)"
arch=('i686' 'x86_64')
url="http://fbsplash.berlios.de"
license=('GPL')
makedepends=('klibc')
depends=('miscsplashutils' 'freetype2' 'libjpeg' 'libpng' 'libmng' 'lcms')
optdepends=('kernel26-fbcondecor: enable console background images'
            'uswsusp-fbsplash: suspend to disk with fbsplash'
            'initscripts-extras-fbsplash: boot/shutdown with fbsplash'
            'python: convert themes from splashy fbsplash')
conflicts=('bootsplash' 'gensplash')
options=( '!makeflags' )
install=fbsplash.install
#source=(http://download.berlios.de/fbsplash/splashutils-${pkgver}.tar.bz2)
source=(http://dev.gentoo.org/~spock/projects/gensplash/archive/splashutils-${pkgver}.tar.bz2)
md5sums=('c722cd4148817b9c50381d9bdc1ea6ef')

build() {
  cd ${srcdir}/splashutils-${pkgver}

  # fix fbcondecor_ctl splash type
  sed -e 's,fbsplash_lib_init(fbspl_bootup),fbsplash_lib_init(fbspl_undef),' -i src/fbcon_decor_ctl.c

  # we do not make use of the gpm stuff in our scripts by now
  ./configure --prefix=/usr --sysconfdir=/etc --enable-fbcondecor --without-gpm --with-mng --with-png --with-ttf --with-ttf-kernel || return 1
  make || return 1
  make DESTDIR=${startdir}/pkg install || return 1

  cd ${pkgdir}

  # fix duplicate slashes to get splash_cache_cleanup grep to work
  sed -r -e 's,^(export spl_.*="/)/+,\1,' -i sbin/splash-functions.sh

  # fix the path to splash_util
  sed -r -e 's,^(export spl_util=)\"/bin/,\1"/sbin/,' -i sbin/splash-functions.sh

  # provide the mountpoint needed by splash-functions.sh
  mkdir -p lib/splash/cache
}
